apply plugin: 'com.android.application'

android {
  compileSdkVersion 27
  buildToolsVersion '27.0.3'

  defaultConfig {
    applicationId "com.squareup.photobooth"
    minSdkVersion 19
    targetSdkVersion 26
    versionCode 2
    versionName "2.0"
    multiDexEnabled true
    resValue "string", "com.twitter.sdk.android.CONSUMER_KEY", "$TWITTER_CONSUMER_KEY"
    resValue "string", "com.twitter.sdk.android.CONSUMER_SECRET", "$TWITTER_CONSUMER_SECRET"
  }
  dexOptions {
    preDexLibraries true
    jumboMode true
    keepRuntimeAnnotatedClasses false
  }
  signingConfigs {
    photobooth {
      storeFile file("../photobooth.keystore")
      storePassword "photobooth"
      keyAlias "photobooth"
      keyPassword "photobooth"
    }
  }
  buildTypes {
    debug {
      signingConfig signingConfigs.photobooth
    }
    release {
      minifyEnabled false
      signingConfig signingConfigs.photobooth
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  packagingOptions {
    exclude 'META-INF/maven/com.squareup.okio/okio/pom.xml'
    exclude 'META-INF/maven/com.squareup.okio/okio/pom.properties'
    pickFirst 'publicsuffixes.gz'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

configurations.all {
  resolutionStrategy {
    eachDependency { details ->
      // Force all primary support libraries to the same version
      if (details.requested.group == 'com.android.support'
          && details.requested.name != 'multidex'
          && details.requested.name != 'multidex-instrumentation') {
        // Force the version that works for you.
        details.useVersion '26.1.0'
      }
    }
  }
}


dependencies {
  def playServicesVersion = '12.0.1'
  def readerSdkVersion = "1.0.3"
  def supportLibVersion = '26.1.0'
  def lifecycleVersion = "1.1.1"
  // ReaderSDK
  implementation "com.squareup.sdk.reader:reader-sdk-$SQUARE_READER_SDK_APPLICATION_ID:$readerSdkVersion"
  runtimeOnly "com.squareup.sdk.reader:reader-sdk-internals:$readerSdkVersion"

  implementation 'com.android.support:design:' + supportLibVersion
  implementation 'com.android.support:exifinterface:' + supportLibVersion
  implementation 'com.android.support:multidex:1.0.3'
  implementation 'com.android.support:recyclerview-v7:' + supportLibVersion
  implementation 'com.android.support:support-v4:' + supportLibVersion


  implementation "android.arch.lifecycle:extensions:$lifecycleVersion"

  implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.8'
  implementation 'com.squareup.retrofit2:retrofit:2.3.0'
  implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
  implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'
  implementation 'com.jakewharton.timber:timber:4.5.1'
  implementation 'com.squareup.sdk:point-of-sale-sdk:2.0'
  implementation 'com.twitter.sdk.android:twitter-core:3.1.1'

  implementation 'com.birbit:android-priority-jobqueue:2.0.1'
  // QR Code scanning
  implementation 'com.dlazaro66.qrcodereaderview:qrcodereaderview:2.0.3'

  // Forcing Play Services versions
  implementation 'com.google.android.gms:play-services-vision:' + playServicesVersion
  implementation 'com.google.android.gms:play-services-base:' + playServicesVersion
  implementation 'com.google.android.gms:play-services-safetynet:' + playServicesVersion
  implementation 'com.google.android.gms:play-services-vision:' + playServicesVersion
  implementation 'com.google.android.gms:play-services-gcm:' + playServicesVersion
}
